dnl configure.ac
dnl
dnl xfwm4 - window manager for the Xfce4 desktop environment
dnl

m4_define([xfwm4_version],[4.3.0])

dnl init autoconf
AC_COPYRIGHT([Copyright (c) 2002-2005
        The Xfce development team. All rights reserved.

Written for Xfce by Olivier Fourdan <fourdan@xfce.org>.])
AC_INIT([xfwm4], [xfwm4_version()], [xfce4-dev@xfce.org])
AM_INIT_AUTOMAKE([xfwm4], [xfwm4_version()])

AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])
AM_CONFIG_HEADER([config.h])

AM_MAINTAINER_MODE

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])

dnl check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_INTLTOOL([0.31], [no-xml])
AC_PROG_LIBTOOL

dnl Check C Compiler Characteristics
AC_C_CONST
AC_C_INLINE

dnl check for standard header files
AC_HEADER_STDC
AC_CHECK_FUNCS([daemon setsid])
AC_CHECK_FUNCS(opendir)

dnl Check for i18n support
XDT_I18N([ar az be bg bn_IN ca cs de el en_GB eo es es_MX et eu fa fi fr gu
	  he hi hu it ja ko lt mr ms nb_NO nl pa pl pt_BR pt_PT ro ru sk
	  sv ta tr uk vi zh_CN zh_TW])

dnl Check for X11 window system
XDT_CHECK_LIBX11_REQUIRE
XDT_CHECK_LIBXPM_REQUIRE
XDT_CHECK_LIBSM

dnl Check for Xext library
AC_CHECK_LIB([Xext], [XShapeCombineShape],
  [
    if ! echo $LIBX11_LIBS | grep -q -- '-lXext'; then
      LIBX11_LIBS="$LIBX11_LIBS -lXext"
    fi
  ], [], [$LIBX11_CFLAGS $LIBX11_LDFLAGS $LIBX11_LIBS])

XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.2.0])
XDT_CHECK_PACKAGE(LIBXFCE4MCS_CLIENT, libxfce4mcs-client-1.0, 4.2.0)
XDT_CHECK_PACKAGE(LIBXFCE4MCS_MANAGER, libxfce4mcs-manager-1.0, 4.2.0)
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.2.0])
XDT_CHECK_PACKAGE(LIBXFCEGUI4, libxfcegui4-1.0, 4.2.0)
XFCE_MCS_PLUGIN([XFCE_MCS_MANAGER], [4.2.0])

dnl
dnl Startup notification support
dnl
XDT_CHECK_OPTIONAL_PACKAGE([LIBSTARTUP_NOTIFICATION],
			   [libstartup-notification-1.0], [0.5],
			   [startup-notification], 
			   [startup notification library], [yes])

dnl
dnl Render support
dnl
if $PKG_CONFIG --exists xrender 2> /dev/null; then
  PKG_CHECK_MODULES(RENDER, xrender)
  have_render="yes"
  AC_DEFINE([HAVE_RENDER], [1], [Define to enable render])
fi

dnl
dnl RANDR extension
dnl (please note that Xrandr requires Xrender - and no, it's not a typo ;)
dnl
AC_ARG_ENABLE([randr],
AC_HELP_STRING([--enable-randr], [try to use the randr extension])
AC_HELP_STRING([--disable-randr], [don't try to use the randr extension]),
  [], [enable_randr=yes])
RANDR_LIBS=
have_xrandr="false"
if test x"$enable_randr" = x"yes"; then
  if test x"$have_render" = x"yes"; then
    ac_CFLAGS="$CFLAGS"
    CFLAGS="$CFLAGS $LIBX11_CFLAGS"
    AC_CHECK_LIB(Xrandr, XRRUpdateConfiguration,
        	 [AC_CHECK_HEADER(X11/extensions/Xrandr.h,
                                  RANDR_LIBS="-lXrandr -lXrender",,
                                  [#include <X11/Xlib.h>])],, 
                  $LIBS $LIBX11_LDFLAGS $LIBX11_LIBS -lXrender -lXext)
    CFLAGS="$ac_CFLAGS"
    AC_DEFINE([HAVE_RANDR], [1], [Define to enable xrandr])
    have_xrandr="yes"
  fi
fi
AC_SUBST([RANDR_LIBS])

dnl
dnl Xcomposite and related extensions
dnl
build_compositor="no"
ENABLE_COMPOSITOR=""
AC_ARG_ENABLE([compositor],
AC_HELP_STRING([--enable-compositor], [enable compositor in xfwm4])
AC_HELP_STRING([--disable-compositor], [disable compositor in xfwm4 (default)]),
  [], [enable_compositor=no])

if test x"$enable_compositor" = x"yes"; then
  if test x"$have_render" = x"yes"; then
    if $PKG_CONFIG --exists xcomposite xfixes xdamage xrender 2> /dev/null; then
      PKG_CHECK_MODULES(COMPOSITOR, xcomposite >= 0.2 xfixes xdamage)
      AC_DEFINE([HAVE_COMPOSITOR], [1], [Define to enable compositor])
      ENABLE_COMPOSITOR="--enable-compositor"
      build_compositor="yes"
    fi
  fi
fi
AC_SUBST(ENABLE_COMPOSITOR)

dnl Check for debugging support
BM_DEBUG_SUPPORT

REVISION=@REVISION@
AC_DEFINE_UNQUOTED([REVISION], "$REVISION", [svn revision number])
AC_SUBST([REVISION])

AC_OUTPUT([ 
xfwm4.spec
Makefile
doc/Makefile
doc/C/Makefile
doc/C/images/Makefile
doc/fr/Makefile
doc/fr/images/Makefile
doc/it/Makefile
doc/it/images/Makefile
defaults/Makefile
icons/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
mcs-plugin/Makefile
po/Makefile.in 
src/Makefile
themes/Makefile
themes/default/Makefile
themes/default.keys/Makefile
themes/default-4.0/Makefile
themes/default-4.2/Makefile
themes/stoneage/Makefile
themes/therapy/Makefile
themes/tyrex/Makefile
themes/wallis/Makefile
])

echo
echo "Build Configuration for $PACKAGE version $VERSION revision $REVISION:"
echo "  Startup notification support:                 $LIBSTARTUP_NOTIFICATION_FOUND"
echo "  Render support:                               $have_render"
echo "  Xrandr support:                               $have_xrandr"
echo "  Embedded compositor:                          $build_compositor"
echo
